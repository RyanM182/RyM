// Detects when a user downgrades a label on a file and that file is then copied to USB

// Timeframe = the time between the label downgrade and file copy event
let timeframe=4h;
let time_range = ago(1d);
MicrosoftPurviewInformationProtection
| where TimeGenerated > time_range
| where LabelEventType == "LabelDowngraded"
| project LabelChangeTime=TimeGenerated, UserId, FileDowngraded=ItemName
| join kind=inner (
    DeviceEvents
    | where TimeGenerated > time_range
    | where ActionType == "UsbDriveMounted"
    | extend DriveLetter = tostring(todynamic(AdditionalFields).DriveLetter)
    | join kind=inner (DeviceFileEvents
        | where TimeGenerated > time_range
        | project TimeGenerated, ActionType, FileName, FolderPath, DeviceId, DeviceName
        | extend FileCopyTime = TimeGenerated
        | where ActionType == "FileCreated"
        | extend FileCopyName = FileName
        | parse FolderPath with DriveLetter '\\' *
        | extend DriveLetter = tostring(DriveLetter)
        )
        on DeviceId, DriveLetter) 
    on $left.FileDowngraded == $right.FileCopyName
| project LabelChangeTime, FileCopyTime, FileDowngraded, DeviceName, AccountName
| where (FileCopyTime - LabelChangeTime) between (0min .. timeframe)


***** V2 *****

MicrosoftPurviewInformationProtection
| where TimeGenerated > ago(90d)
| where LabelEventType == "LabelDowngraded"
| extend FileName = tostring(split(ObjectId,"/")[-1])
| project LabelChangeTime=TimeGenerated, UserId, FileName
| join kind=inner (
    DeviceEvents
    | where TimeGenerated > ago(90d)
    | where ActionType == "UsbDriveMounted"
    | extend DriveLetter = tostring(todynamic(AdditionalFields).DriveLetter)
    | join kind=inner (
    DeviceFileEvents
        | where TimeGenerated > ago(90d)
        | project TimeGenerated, ActionType, FileName, FolderPath, DeviceId, DeviceName
        | extend FileCopyTime = TimeGenerated
        | where ActionType == "FileCreated"
        | extend FileCopyName = FileName
        | parse FolderPath with DriveLetter '\\' *
        | extend DriveLetter = tostring(DriveLetter)
        )
        on DeviceId, DriveLetter
        )
    on FileName
| project LabelChangeTime, FileCopyTime, FileName, DeviceName, AccountName